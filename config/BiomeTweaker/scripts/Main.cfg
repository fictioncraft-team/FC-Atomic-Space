#createBiome
Tweaker.setScriptStage("BIOME_REGISTRY")
Tweaker.createBiome("overworld_core","minecraft:hell")
Tweaker.createBiome("underground_overworld","minecraft:plains")
Tweaker.createBiome("overworld_2464","minecraft:plains")

core = forBiomes("biometweaker:overworld_core")
under = forBiomes("biometweaker:underground_overworld")
overworld = forBiomes("biometweaker:overworld_2464")

core.set("name", "Overworld Core")
under.set("name", "Overworld-2464 Mantle")
overworld.set("name", "Overworld-2464 Crust")


#variableBlocks
top = forBlock("minecraft:ice")
filler = forBlock("contenttweaker:ice_dirt")
afiller = forBlock("contenttweaker:ice_stone")
deco = forBlock("contenttweaker:ice_gravel")
liquid = forBlock("thermalfoundation:fluid_cryotheum")
rep = forBlock("minecraft:packed_ice")
rep2 = forBlock("chisel:ice1")
rep2.setProperty("variation", "9")
permaFrost = forBlock("quark:biome_cobblestone")
permaFrost.setProperty("variant", "icy_stone")
reps = forBlock("minecraft:air")
repsl = forBlock("quark:icy_stone_brick_slab")
netherbrickslab = forBlock("minecraft:stone_slab")
netherbrickslab.setProperty("variation", "6")
magmarep = forBlock("ice_magma")
obsidianrep = forBlock("environmentalmaterials:basalt")
fencerep = forBlock("immersiveposts:fence_copper")


#setBlocksFeatures
Tweaker.setPlacementStage("BIOME_BLOCKS")
overworld.set("topBlock", top)
overworld.set("fillerBlock", filler)
overworld.addActualFillerBlock(afiller)
overworld.set("temperature", -0.0)
overworld.set("enableSnow", true)
overworld.removeFeature("DUNGEON")
overworld.set("genVillages", false)
overworld.set("genStrongholds", false)
overworld.removeOre("all")
overworld.removeDicTypes("PLAINS")
overworld.addDicTypes("COLD")
overworld.set("isSpawnBiome", true)

under.set("genVillages", false)
under.set("genStrongholds", false)
under.set("temperature", -0.1)
under.removeOre("all")
under.removeDicTypes("PLAINS")
under.addDicTypes("COLD")
under.addDicTypes("DEAD")
under.removeFeature("DUNGEON")

core.set("genVillages", false)
core.set("genStrongholds", false)
core.removeFeature("DUNGEON")
core.removeDicTypes("HOT")
core.removeDicTypes("DRY")
core.removeDicTypes("NETHER")
core.addDicTypes("COLD")
core.set("temperature", -0.3)

#replacement
Tweaker.setPlacementStage("PRE_POPULATE")

underSpikesRep = newBlockReplacement()
underSpikesRep.set("block", "twilightforest:aurora_pillar")
underSpikesRep.set("minY", 85)
underSpikesRep.set("maxY", 120)
under.registerGenBlockRep("minecraft:cobblestone", underSpikesRep)

underPillarRep = newBlockReplacement()
underPillarRep.set("block", "contenttweaker:ice_stone")
underPillarRep.set("minY", 62)
underPillarRep.set("maxY", 90)
under.registerGenBlockRep("minecraft:cobblestone", underPillarRep)
under.registerGenBlockRep(8, "twilightforest:aurora_pillar", underPillarRep, 2)

underRoofRep = newBlockReplacement()
underRoofRep.set("block", "chisel:ice")
underRoofRep.set("minY", 118)
underRoofRep.set("maxY", 123)
under.registerGenBlockRep("minecraft:cobblestone", underRoofRep)
under.registerGenBlockRep(7, "twilightforest:aurora_pillar", underRoofRep, 3)

underRoofRepTwo = newBlockReplacement()
underRoofRepTwo.set("block", "minecraft:packed_ice")
underRoofRepTwo.set("minY", 122)
underRoofRepTwo.set("maxY", 240)
under.registerGenBlockRep("minecraft:cobblestone", underRoofRepTwo)
under.registerGenBlockRep(3, "chisel:ice", underRoofRepTwo, 7)

groundSpike = newBlockReplacement()
groundSpike.set("block", permaFrost)
groundSpike.set("minY", 61)
groundSpike.set("maxY", 65)
under.registerGenBlockRep("minecraft:cobblestone", groundSpike)
under.registerGenBlockRep(4, "contenttweaker:ice_stone", groundSpike, 6)
under.registerGenBlockRep(3, "quark:slate", groundSpike, 7)


#replacementLast
Tweaker.setPlacementStage("PRE_DECORATE")

overworld.registerGenBlockRep("minecraft:gravel", deco)
overworld.registerGenBlockRep("minecraft:dirt", filler)
overworld.registerGenBlockRep("minecraft:stone", afiller)
overworld.registerGenBlockRep("minecraft:sand", top)

under.registerGenBlockRep("minecraft:gravel", deco)
under.registerGenBlockRep("minecraft:dirt", filler)
under.registerGenBlockRep("minecraft:stone", afiller)
under.registerGenBlockRep("minecraft:sand", top)
under.registerGenBlockRep("minecraft:water", "nuclearcraft:fluid_slurry_ice")

core.registerGenBlockRep("minecraft:netherrack", rep)
core.registerGenBlockRep("minecraft:nether_brick", rep2)
core.registerGenBlockRep("minecraft:nether_brick_stairs", reps)
core.registerGenBlockRep(netherbrickslab, repsl)
core.registerGenBlockRep("minecraft:lava", liquid)
core.registerGenBlockRep("minecraft:gravel", deco)
core.registerGenBlockRep("minecraft:stone", afiller)
core.registerGenBlockRep("minecraft:obsidian", obsidianrep)
core.registerGenBlockRep("minecraft:magma", magmarep)
core.registerGenBlockRep("minecraft:nether_brick_fence", fencerep)


#setMobSpawn
Tweaker.setScriptStage("INIT")

overworld.removeAllSpawns("CREATURE")
overworld.removeAllSpawns("MONSTER")
overworld.removeAllSpawns("WATER_CREATURE")
overworld.removeAllSpawns("CAVE_CREATURE")
overworld.addSpawn("net.minecraft.entity.monster.EntityStray", "MONSTER", 80, 4, 4)
overworld.addSpawn("vazkii.quark.world.entity.EntityDweller", "MONSTER", 40, 2, 3)
overworld.addSpawn("net.minecraft.entity.monster.EntityCreeper", "MONSTER", 50, 1, 4)
overworld.addSpawn("net.minecraft.entity.monster.EntityZombieVillager", "MONSTER", 50, 2, 4)
overworld.addSpawn("net.minecraft.entity.monster.EntityZombie", "MONSTER", 50, 3, 4)
overworld.addSpawn("net.minecraft.entity.monster.EntitySpider", "MONSTER", 50, 3, 4)
overworld.addSpawn("cofh.thermalfoundation.entity.monster.EntityBlizz", "MONSTER", 10, 3, 5)
overworld.addSpawn("mekanism.common.entity.EntityBabySkeleton")

under.removeSpawn("net.minecraft.entity.monster.EntityWitch", "MONSTER")
under.removeSpawn("net.minecraft.entity.monster.EntityZombieVillager", "MONSTER")
under.removeSpawn("teamroots.embers.entity.EntityAncientGolem", "MONSTER")
under.removeSpawn("net.minecraft.entity.monster.EntityEnderman", "MONSTER")
under.addSpawn("net.minecraft.entity.monster.EntityEnderman", "MONSTER", 40, 2, 4)
under.removeAllSpawns("CAVE_CREATURE")
under.removeAllSpawns("WATER_CREATURE")
under.removeAllSpawns("CREATURE")

core.removeAllSpawns("MONSTER")
core.removeAllSpawns("CREATURE")
core.addSpawn("cofh.thermalfoundation.entity.monster.EntityBlizz", "MONSTER", 20, 3, 5)
core.addSpawn("net.minecraft.entity.monster.EntityEnderman", "MONSTER", 50, 2, 4)
core.addSpawn("cofh.thermalfoundation.entity.monster.EntityBasalz", "MONSTER", 10, 1, 3)
core.addSpawn("net.minecraft.entity.monster.EntityStray", "MONSTER", 70, 2, 4)